plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {


    compileSdk 36
    ndkVersion = "27.0.12077973"

    defaultConfig {
        multiDexEnabled true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.vtn.global.base.flutter"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion 36
        versionCode 100
        versionName '1.0.0'
        multiDexEnabled true

    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    namespace 'com.vtn.global.base.flutter'
}

flutter {
    source '../..'
}


dependencies {
    implementation 'androidx.activity:activity-ktx:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-common-jvm:2.8.7'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    // firebase sdks
    implementation(platform("com.google.firebase:firebase-bom:34.0.0"))
    implementation("com.google.firebase:firebase-crashlytics")
    implementation("com.google.firebase:firebase-analytics")
    implementation 'com.google.firebase:firebase-messaging'

//    implementation 'com.google.android.gms:play-services-ads:24.6.0'
//    implementation 'com.google.ads.mediation:facebook:6.20.0.1'
//    implementation 'com.facebook.android:facebook-android-sdk:18.1.3'
//    implementation 'com.facebook.android:audience-network-sdk:6.20.0'

    implementation project(path: ':documents-reader-lib')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.work:work-runtime-ktx:2.9.0"

    implementation 'com.github.tinhbka:adjust-helper:0.1.6'
}
afterEvaluate {
    def currentDate = new Date().format("yyyy-MM-dd")
    def versionName = project.android.defaultConfig.versionName

    def flavorNames = []
    project.android.productFlavors.all { flavor ->
        flavorNames << flavor.name
    }
    flavorNames += [""] // default

    flavorNames.each { flavor ->
        def flavorCap = flavor.capitalize()
        def taskName = flavor ? "bundle${flavorCap}Release" : "bundleRelease"
        def outputDir = "$projectDir/${flavor}/release"
        def inputAab = file("$outputDir/app-${flavor ? flavor.toLowerCase() + "-" : ""}release.aab")
        def renamedAab = file("$outputDir/app${flavor ? "-${flavor}" : ""}-v${versionName}-${currentDate}.aab")

        tasks.matching { it.name == taskName }.configureEach {
            doLast {
                if (inputAab.exists()) {
                    println "‚úÖ Renaming AAB: ${inputAab.name} ‚Üí ${renamedAab.name}"
                    inputAab.renameTo(renamedAab)

                    // T·ª± ƒë·ªông m·ªü th∆∞ m·ª•c ch·ª©a file AAB
                    def folderPath = renamedAab.parent
                    println "üìÇ Opening folder: $folderPath"

                    if (System.properties['os.name'].toLowerCase().contains('windows')) {
                        exec {
                            commandLine 'cmd', '/c', 'start', '', folderPath
                        }
                    } else if (System.properties['os.name'].toLowerCase().contains('mac')) {
                        exec {
                            commandLine 'open', folderPath
                        }
                    } else {
                        exec {
                            commandLine 'xdg-open', folderPath
                        }
                    }
                } else {
                    println "‚ùå AAB not found: ${inputAab}"
                }
            }
        }
    }

}
// ----- BEGIN flavorDimensions (autogenerated by flutter_flavorizr) -----
apply from: "flavorizr.gradle"
// ----- END flavorDimensions (autogenerated by flutter_flavorizr) -----